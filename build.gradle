/**
 * Copyright (c) Dell Inc., or its subsidiaries. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:${shadowGradlePlugin}"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${bintrayPluginVersion}"
        classpath "org.ajoberstar:gradle-git:${gradleGitPluginVersion}"
    }
}

plugins {
    id "com.github.maiflai.scalatest" version "0.25"
}

apply plugin: 'eclipse'
apply from: 'gradle/java.gradle'
apply from: "gradle/scala.gradle"
apply from: 'gradle/maven.gradle'

apply from: 'gradle/bintray.gradle'
apply plugin: 'org.ajoberstar.grgit'

group = "io.pravega"

version = connectorVersion

description = """Pravega Spark Connector"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    if (findProperty("repositoryUrl")) {
        maven {
            url findProperty("repositoryUrl")
        }
    }
    else {
        jcenter()
        mavenCentral()
        maven { url "https://repository.apache.org/snapshots" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "https://oss.jfrog.org/jfrog-dependencies" }
    }
}

configurations {
    shadowOnly {
    }
}

dependencies {
    compile group: 'io.pravega', name: 'pravega-client', version: pravegaVersion
    compile group: 'org.scala-lang', name: 'scala-library', version: scalaVersion
    compile group: 'org.scala-lang', name: 'scala-reflect', version: scalaVersion
    compile group: 'org.scala-lang', name: 'scala-compiler', version: scalaVersion
    compile group: 'org.scala-lang.modules', name: 'scala-java8-compat_2.11', version: scalaJava8CompatVersion
    compile group: 'com.jsuereth', name: 'scala-arm_2.11', version: '2.0'
    compile group: 'org.scala-lang.modules', name: 'scala-java8-compat_2.11', version: '0.9.1'

    compileOnly group: 'org.apache.spark', name: 'spark-sql_2.11', version: sparkVersion

    shadowOnly group: 'org.apache.spark', name: 'spark-sql_2.11', version: sparkVersion

    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group: 'io.pravega', name: 'pravega-standalone', version: pravegaVersion
    testCompile group: 'org.scalatest', name: 'scalatest_2.11', version: scalaTestVersion
    testRuntimeOnly group: 'org.pegdown', name: 'pegdown', version: pegDownVersion
    testCompile group: 'org.apache.spark', name: 'spark-sql_2.11', version: sparkVersion
    testCompile group: 'org.apache.spark', name: 'spark-core_2.11', version: sparkVersion, classifier: 'tests'
    testCompile group: 'org.apache.spark', name: 'spark-catalyst_2.11', version: sparkVersion, classifier: 'tests'
    testCompile group: 'org.apache.spark', name: 'spark-sql_2.11', version: sparkVersion, classifier: 'tests'
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jacksonVersion
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    testCompile group: 'com.fasterxml.jackson.module', name: 'jackson-module-scala_2.11', version: jacksonVersion
    testCompile group: 'io.pravega', name: 'pravega-test-integration', version: pravegaVersion
}

configurations {
    shadowOnly {
    }
    compile.exclude group: 'ch.qos.logback', module: 'logback-classic'
    compile.exclude group: 'com.sun.jersey', module: 'jersey-core'
}

test {
    dependsOn 'cleanTest'
}

shadowJar {
    classifier = null
    version = version
    zip64 = true
    mergeServiceFiles()

    // relocate pravega client's dependencies to minimize conflicts
    relocate "org.apache.commons", "io.pravega.shaded.org.apache.commons"
    relocate "com.google", "io.pravega.shaded.com.google"
    relocate "io.grpc", "io.pravega.shaded.io.grpc"
    relocate "com.squareup.okhttp", "io.pravega.shaded.com.squareup.okhttp"
    relocate "okio", "io.pravega.shaded.okio"
    relocate "io.opencensus", "io.pravega.shaded.io.opencensus"
    relocate "io.netty", "io.pravega.shaded.io.netty"
    relocate 'META-INF/native/libnetty', 'META-INF/native/libio_pravega_shaded_netty'
    relocate 'META-INF/native/netty', 'META-INF/native/io_pravega_shaded_netty'
}

javadoc {
    title = "Pravega Spark Connector"
    failOnError = false
    exclude "**/impl/**";
}

def getProjectVersion() {
    String ver = connectorVersion
    if (ver.contains("-SNAPSHOT")) {
        String versionLabel = ver.substring(0, ver.indexOf("-SNAPSHOT"))
        def count = grgit.log(includes:['HEAD']).size()
        def commitId = "${grgit.head().abbreviatedId}"
        ver = versionLabel + "-" + count + "." + commitId + "-SNAPSHOT"
    }
    return ver
}
